ODROIDN2-UBOOT-CONFIG

setenv bootlabel "Android Pie (64Bit)"

###################
# Default values
###################
setenv hdmimode "1080p60hz"
setenv voutmode "hdmi"
setenv display_autodetect "true"
setenv backlight_pwm "yes"
setenv zoom_rate "100"
setenv colorattribute "444,8bit"
setenv osd_reverse "0"
setenv video_reverse "0"
setenv suspend_hdmiphy "1"
setenv cvbsmode "576cvbs"
setenv disablehpd "false"
setenv disable_vu7 "false"
setenv touch_invert_x "false"
setenv touch_invert_y "false"
setenv test_mt_vid "0000"
setenv test_mt_pid "0000"
setenv max_freq_big "1800"
setenv max_freq_little "1896"
setenv enable_wol "0"
setenv heartbeat "1"
setenv sg_tablesize "2"
setenv prevent_sleep "0"

###################
# ini load & read
###################

setenv iniloadaddr "0x4001000"
load ${devtype} ${devnum} ${iniloadaddr} env.ini
ini display ${iniloadaddr}
ini cpu ${iniloadaddr}
ini etc ${iniloadaddr}

###################
# Boot args
###################

# Init args
setenv rootopt "rootfstype=ramfs init=/init"
setenv consoleopt "console=ttyS0,115200n8 no_console_suspend earlyprintk=aml-uart,0xff803000 quiet"
setenv ramoopsopt "ramoops.pstore_en=1 ramoops.record_size=0x8000 ramoops.console_size=0x4000"
setenv touchopt "touch_invert_x=${touch_invert_x} touch_invert_y=${touch_invert_y}"
setenv displayopt "colorattribute=${colorattribute} disablehpd=${disablehpd}"
setenv testmtopt "test_mt_vid=${test_mt_vid} test_mt_pid=${test_mt_pid} backlight_pwm=${backlight_pwm}"
setenv androidopt "androidboot.selinux=permissive androidboot.firstboot=${firstboot} jtag=disable androidboot.hardware=odroidn2 otg_device=1 buildvariant=eng"
setenv miscopt "disable_vu7=${disable_vu7} ${touchopt} zoom_rate=${zoom_rate} ${testmtopt} suspend_hdmiphy=${suspend_hdmiphy} prevent_sleep=${prevent_sleep} usb-xhci.tablesize=${sg_tablesize}"

# dtbs & boot addr
setenv dtb_mem_addr 0x1000000
setenv loadaddr 0x1080000

# logo
setenv display_layer "osd0"
setenv fb_addr "0x3d800000"
setenv logo "${display_layer},loaded,${fb_addr}"

# Boot Arguments
if test "${display_autodetect}" = "true"; then hdmitx edid; fi
# TODO -> if test "${display_autodetect}" = "true"; then usb pwren; hdmitx edid; fi
if test "${hdmimode}" = "custombuilt"; then setenv cmode "modeline=${modeline} customwidth=${customwidth} customheight=${customheight}"; fi

setenv initargs "${rootopt} ${consoleopt} ${ramoopsopt} max_freq_a73=${max_freq_big} max_freq_a53=${max_freq_little} ${cmode} ${miscopt}"

setenv bootargs "${initargs} ${androidopt} ${displayopt} logo=${logo} cvbsmode=${cvbsmode} hdmimode=${hdmimode} voutmode=${voutmode} cvbscable=${cvbscable} osd_reverse=${osd_reverse} video_reverse=${video_reverse} gpiopower=${gpiopower} enable_wol=${enable_wol}"

movi read boot 0 ${loadaddr}
movi read dtbs 0 ${dtb_mem_addr}
fdt addr ${dtb_mem_addr}
if test "${heartbeat}" = "0"; then fdt set /leds/blueled linux,default-trigger "none"; fi
booti ${loadaddr} - ${dtb_mem_addr}
bootm
